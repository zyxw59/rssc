use ast::{Pattern, Repeater};

grammar;

pub Pattern: Pattern = {
    PatternConcat,
    <v: (<PatternConcat> "|")+> <e: PatternConcat> => {
        let mut v = v;
        v.push(e);
        Pattern::Alternate(v)
    },
};

PatternConcat: Pattern = {
    PatternRepeat,
    <v: PatternRepeat+> <e: PatternRepeat> => {
        let mut v = v;
        v.push(e);
        Pattern::Concat(v)
    },
    => Pattern::Empty,
};

PatternRepeat: Pattern = {
    PatternToken,
    <PatternToken> "?" => Pattern::Repeat(Box::new(<>), Repeater::ZeroOrOne(true)),
    <PatternToken> "*" => Pattern::Repeat(Box::new(<>), Repeater::ZeroOrMore(true)),
    <PatternToken> "+" => Pattern::Repeat(Box::new(<>), Repeater::OneOrMore(true)),
    <PatternToken> "??" => Pattern::Repeat(Box::new(<>), Repeater::ZeroOrOne(false)),
    <PatternToken> "*?" => Pattern::Repeat(Box::new(<>), Repeater::ZeroOrMore(false)),
    <PatternToken> "+?" => Pattern::Repeat(Box::new(<>), Repeater::OneOrMore(false)),
};

PatternToken: Pattern = {
    "." => Pattern::AnyChar,
    "#" => Pattern::WordBoundary,
    CATEGORY => Pattern::parse_category(<>).unwrap(),
    GROUP_START <Pattern> ")" => <>,
    LITERALS => Pattern::Literal(String::from(<>)),
};

match {
    r"\((\?:)?" => GROUP_START,
} else {
    r"\{([0-9]+:)?\w+\}" => CATEGORY,
    r"([^.#{}()|?*+\\]|\\[.#{}()|?*+\\])+" => LITERALS,
    _
}

